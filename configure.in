AC_PREREQ([2.61])

DNA define([AC_CACHE_LOAD], )dnl
dnl define([AC_CACHE_SAVE], )dnl

AC_INIT(Makefile)


dnl work out who the cpu, vendor and OS are
AC_CANONICAL_SYSTEM
AC_DEFINE_UNQUOTED(CPU_VENDOR_OS, "$host")

AC_ERLANG_NEED_ERL
AC_PATH_PROG(ERLC, erlc)

AC_SUBST(ERL)
AC_SUBST(ERLC)

AC_ARG_WITH(defaultcharset,
AS_HELP_STRING([--with-defaultcharset=String], [specify default charset, i.e UTF-8]))


if test "x$with_defaultcharset" = "x" ; then
   DEFAULT_CHARSET=undefined
else
   DEFAULT_CHARSET="\"${with_defaultcharset}\""
fi

AC_SUBST(DEFAULT_CHARSET)



AC_ARG_WITH(extrainclude,
AS_HELP_STRING([--with-extrainclude=DIR], [dir to include if e.g. pam is installed in some odd place]))


if test "x$with_extrainclude" = "x" ; then
   EXTRAINCLUDE=
else
   EXTRAINCLUDE=-I${with_extrainclude}
   CFLAGS="$CFLAGS $EXTRAINCLUDE"
   CPPFLAGS="$CPPFLAGS $EXTRAINCLUDE"
fi

AC_SUBST(EXTRAINCLUDE)


AC_ARG_ENABLE(pam, AS_HELP_STRING([--disable-pam], [disables pam auth]))
if test "x$enable_pam" != "xno"; then
    EPAM=../priv/epam
else
    EPAM=
fi
AC_SUBST(EPAM)



dnl ----------------------------------------------------------------------
dnl
dnl BT_ERL_LIB_VSN figures out version of an erlang application
dnl and substitutes it as "APPNAME"_VSN
dnl
dnl Note that in the non-erts case, if checking ERLDIR for a versioned
dnl directory fails, it could be because the user is using a git clone
dnl of the Erlang/OTP repository. In that case, check for a vsn.mk file
dnl in the application subdirectory under the repository lib directory,
dnl and if found use the version found there.

AC_DEFUN([BT_ERL_LIB_VSN],
[
    AC_MSG_CHECKING([$1 version])
    AC_CACHE_VAL(bt_cv_$1_vsn,
    [case "$1" in
         erts)
             bt_cv_$1_vsn=`"${ERL}" -noshell -eval 'io:format("~s~n",[[erlang:system_info(version)]]), erlang:halt().' | tail -1`
             ;;
         *)
             bt_cv_$1_vsn=`ls -1 "${ERLDIR}" "${ERLDIR}/lib" | \
                     awk -F- '/^$1-/ { if ([$]2 > v) v=[$]2; }
                              END    { print v; }'`
             if test -z "$bt_cv_$1_vsn" ; then
                 bt_cv_$1_vsn=`sed -e 's/^.*= *//' "${ERLDIR}/lib/$1/vsn.mk" 2>/dev/null`
             fi
             ;;
     esac
     case "$bt_cv_$1_vsn" in
         "")
             AC_MSG_ERROR("Could not figure out version Erlang library: $1")
             ;;
         *)
             ;;
     esac])
    translit($1, [a-z], [A-Z])_VSN=$bt_cv_$1_vsn
    AC_SUBST(translit($1, [a-z], [A-Z])_VSN)dnl
    AC_MSG_RESULT($bt_cv_$1_vsn)
])dnl




ERLDIR=`"${ERL}" -noshell -eval 'io:format("~s~n",[[code:root_dir()]]), erlang:halt().' | tail -1`
ERLBINDIR="${ERLDIR}/bin"
ERL_DLL_LIB="${ERLDIR}/usr/lib/erl_dll.lib"


if test ! -d "$ERLDIR" ; then
        AC_MSG_ERROR([Broken Erlang installation, $ERLDIR does not exist!])
fi
AC_SUBST(ERLDIR)
AC_SUBST(ERLBINDIR)
AC_SUBST(ERL_DLL_LIB)

BT_ERL_LIB_VSN(erts)

set `echo $bt_cv_erts_vsn | sed 's/\./ /g'`
ERTS_MAJOR=$1
ERTS_MINOR=$2
ERTS_MAINT=${3:-0}
AC_SUBST(ERTS_MINOR)
AC_SUBST(ERTS_MAJOR)

if test $ERTS_MAJOR -gt 5 -o '(' $ERTS_MAJOR -eq 5 -a $ERTS_MINOR -gt 6 ')'; then
   BITS_SUPPORT=true
fi
AC_SUBST(BITS_SUPPORT)

BT_ERL_LIB_VSN(kernel)
BT_ERL_LIB_VSN(stdlib)
BT_ERL_LIB_VSN(sasl)
BT_ERL_LIB_VSN(ssl)
BT_ERL_LIB_VSN(mnesia)


AC_PROG_CC
AC_PROG_GCC_TRADITIONAL


dnl ===============================================================
dnl Checks for programs.
dnl ===============================================================



case "$host_os" in
     *cygwin*)
        WIN32=win32
            cat >> confdefs.h << EOF
#define WIN32 1
EOF
            LD_SHARED="$CC -shared"
            DLL=dll
            DLL_LIBS="-lc \"$ERL_DLL_LIB\""


        EXE=.exe
        AC_SUBST(EXE)

        DLL=dll
        AC_SUBST(DLL)

        OBJ=obj
        AC_SUBST(OBJ)
        ;;
      *)


        LINKER=$CC
        AC_SUBST(LINKER)

        AC_SUBST(CFLAGS)

        OUT="-o "
        AC_SUBST(OUT)

        DLL=so
        AC_SUBST(DLL)
        OBJ=o
        AC_SUBST(OBJ)


        ;;
esac


AC_SUBST(DLL_LIBS)

AC_PROG_MAKE_SET
AC_PROG_INSTALL


dnl Make LaTeX and related commands configurable
dnl
dnl We might want to check whether latex is pdflatex and creates
dnl PDF instead of DVI without being given -output-format=dvi.
dnl However, this at least allows to give the working command at
dnl configure time.
AC_ARG_VAR([DVILATEX], [(pdf)latex command creating DVI file])
AC_CHECK_PROG([DVILATEX], [latex], [latex])

AC_ARG_VAR([DVIPS], [dvips command])
AC_CHECK_PROG([DVIPS], [dvips], [dvips])

AC_ARG_VAR([PDFLATEX], [(pdf)latex command creating PDF file])
AC_CHECK_PROG([PDFLATEX], [pdflatex], [pdflatex])

AC_ARG_VAR([EPSTOPDF], [epstopdf command])
AC_CHECK_PROG([EPSTOPDF], [epstopdf], [epstopdf])

AS_IF([test "x$DVILATEX" = "xno" || test "x$DVIPS" = "xno"],
      [AC_SUBST([COND_DVILATEX], ["-"])],
      [AC_SUBST([COND_DVILATEX], [""])])

AS_IF([test "x$PDFLATEX" = "xno" || test "x$EPSTOPDF" = "xno"],
      [AC_SUBST([COND_PDFLATEX], ["-"])],
      [AC_SUBST([COND_PDFLATEX], [""])])


dnl Sigh, pam headers do not seem to have a standard place. On FreeBSD and
dnl Linuces I've seen they are in /usr/include/security, but MacOS has them
dnl in /usr/include/pam.
if test "x$enable_pam" != "xno"; then
    AC_CHECK_HEADERS([security/pam_appl.h pam/pam_appl.h pam_appl.h],[break])
    if test "x$ac_cv_header_security_pam_appl_h" = "xyes" ; then
        CFLAGS="$CFLAGS -I/usr/include/security"
    elif test "x$ac_cv_header_pam_pam_appl_h" = "xyes" ; then
        CFLAGS="$CFLAGS -I/usr/include/pam"
    elif test "x$ac_cv_header_pam_appl_h" = "xno" ; then
        AC_MSG_ERROR("Could not find pam_appl.h required for pam authentication")
    fi
    AC_CHECK_LIB(pam,pam_start,[],[AC_MSG_ERROR("Could not find pam library")])
fi

case "$host_os" in
        *linux*)
           AC_DEFINE(LINUX)
           AC_LANG(Erlang)
           AC_RUN_IFELSE(
               [AC_LANG_PROGRAM([],[dnl
                   halt(try erlang:system_info({wordsize,external}) of
                            8 -> 0; 4 -> 1
                        catch _:badarg ->
                            case erlang:system_info(wordsize) of
                                8 -> 0; 4 -> 1
                            end end)])],
	       [AC_MSG_NOTICE(found 64-bit Erlang)
                   case "$host_cpu" in
                       x86_64) CBIT= ;;
                       *) CBIT=-m64 ;;
                   esac],
	       [AC_MSG_NOTICE(found 32-bit Erlang)
                   case "$host_cpu" in
                       i?86) CBIT= ;;
                       armv6l) CBIT= ;;
                       *) CBIT=-m32 ;;
                   esac])
           CFLAGS="$CFLAGS $CBIT"
           LD_SHARED="$CC $CBIT -shared"
           AC_SUBST(LD_SHARED)
           FPIC=-fpic
           AC_SUBST(FPIC)
           ;;

        *solaris*)
           AC_DEFINE(SOLARIS)
           LD_SHARED="ld -G"
           AC_SUBST(LD_SHARED)
           LIBS="$LIBS -lsocket -lnsl -lresolv"
           AC_SUBST(LIBS)
           FPIC=-fpic
           AC_SUBST(FPIC)
           ;;

        *bsd*)
           AC_DEFINE(BSD)
           case "$host_os" in
                freebsd*)
                    ;;
                bsdi*)
                    BSDI=bsdi
                    AC_DEFINE(BSDI)
                    AC_SUBST(BSDI)
                    ;;
           esac
           LD_SHARED="$CC -shared"
           AC_SUBST(LD_SHARED)
           FPIC=-fpic
           AC_SUBST(FPIC)
           ;;
         *darwin*)
           case "$host_os" in
               darwin1?*)
                   AC_LANG(Erlang)
                   AC_RUN_IFELSE(
                       [AC_LANG_PROGRAM([],[dnl
                           halt(try erlang:system_info({wordsize,external}) of
                                    8 -> 0; 4 -> 1
                                catch _:badarg ->
                                    case erlang:system_info(wordsize) of
                                        8 -> 0; 4 -> 1
                                    end end)])],
	               [AC_MSG_NOTICE(found 64-bit Erlang)
                           CBIT=-m64],
	               [AC_MSG_NOTICE(found 32-bit Erlang)
                           CBIT=-m32])
                   ;;
               *)
                   CBIT=""
                   ;;
           esac
           CFLAGS="$CFLAGS $CBIT"
           LD_SHARED="$CC $CBIT -bundle -fPIC -flat_namespace -undefined suppress"
           AC_SUBST(LD_SHARED)
           FPIC=-fPIC
           AC_SUBST(FPIC)
           ;;
         *cygwin*)
           AC_PATH_PROG(WERL, werl)
           AC_SUBST(WERL)
           AC_DEFINE(WIN32)
           FPIC=-fpic
           AC_SUBST(FPIC)
           AC_SUBST(WIN32)
           ;;
        *)
           AC_MSG_RESULT(unknown)
           AC_MSG_ERROR(Unsupported host OS!)
           ;;
esac

dnl check sendfile support:
dnl   - HAVE_SENDFILE: say if sendfile syscall is supported
dnl   - HAVE_ERLANG_SENDFILE: say if file:sendfile/5 exists

HAVE_SENDFILE=false
HAVE_ERLANG_SENDFILE=false

AC_LANG(C)
AC_CHECK_LIB([c],[sendfile],[HAVE_SENDFILE=true])

file_sendfile=`"${ERL}" -noshell -eval 'code:ensure_loaded(file), io:format("~p~n",[[erlang:function_exported(file,sendfile,5)]]), erlang:halt().' | tail -1`
if test "$file_sendfile" = true -a \
   '(' $ERTS_MAJOR -gt 5 -o \
     '(' $ERTS_MAJOR -eq 5 -a $ERTS_MINOR -gt 9 ')' -o \
     '(' $ERTS_MAJOR -eq 5 -a $ERTS_MINOR -eq 9 -a $ERTS_MAINT -ge 1 ')' ')' ; then
   AC_MSG_NOTICE(found file:sendfile/5)
   HAVE_ERLANG_SENDFILE=true
fi

AC_SUBST(HAVE_SENDFILE)
AC_SUBST(HAVE_ERLANG_SENDFILE)

HAVE_CRYPTO_HASH=false
crypto_hash=`"${ERL}" -noshell -eval 'code:ensure_loaded(crypto), io:format("~p~n",[[erlang:function_exported(crypto,hash,2)]]), erlang:halt().' | tail -1`
if test "$crypto_hash" = true; then
   HAVE_CRYPTO_HASH=true
   AC_MSG_NOTICE(found crypto:hash/2)
fi
AC_SUBST(HAVE_CRYPTO_HASH)

YTOP=`pwd`
AC_SUBST(YTOP)
AC_OUTPUT(include.mk)
AC_OUTPUT(src/yaws_charset.hrl)
AC_OUTPUT(test/support/include.mk)
AC_OUTPUT(test/support/include.sh)

dnl pkg-config support
. ./vsn.mk
AC_SUBST(YAWS_VSN)
AC_OUTPUT(yaws.pc)
